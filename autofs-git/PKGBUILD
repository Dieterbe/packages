# Maintainer: Dieter Plaetinck <dieter@plaetinck.be>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs-git
pkgver=20110514
pkgrel=1
pkgdesc='A kernel-based automounter for Linux.'
arch=('i686' 'x86_64')
url='http://freshmeat.net/projects/autofs'
license=('GPL2')
depends=('libldap' 'libxml2' 'krb5')
makedepends=('git')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/conf.d/autofs')
options=(!makeflags)
install='autofs.install'
source=('autofs'
        'autofs.conf.d'
        'auto.master'
        'auto.misc')
md5sums=('e307bf6d2638e46eeb916cf42fe029b2'
         '47f597c870410055e0fdb66103daf928'
         'a6cefb591e77b31b79dbb7243646c96b'
         'd8a15ec9186c5c0b36e5cea1e2739e8a')
conflicts=('autofs')
replaces=('autofs')
_gitroot="git://git.kernel.org/pub/scm/linux/storage/autofs/autofs.git"
_gitname="master"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i "s:SUBDIRS = lib daemon modules man samples:SUBDIRS = lib daemon modules man:" \
    Makefile.rules

  ./configure --prefix=/usr --sysconfdir=/etc/autofs --with-mapdir=/etc/autofs --without-hesiod \
    --enable-ignore-busy
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make INSTALLROOT="${pkgdir}" install

  install -Dm0644 "${srcdir}/auto.master" "${pkgdir}/etc/autofs/auto.master"
  install -Dm0644 "${srcdir}/auto.misc" "${pkgdir}/etc/autofs/auto.misc"
  install -Dm0755 "${srcdir}/autofs" "${pkgdir}/etc/rc.d/autofs"
  install -Dm0644 "${srcdir}/autofs.conf.d" "${pkgdir}/etc/conf.d/autofs"
}
